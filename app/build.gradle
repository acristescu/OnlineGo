apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "io.zenandroid.onlinego"
        minSdkVersion 19
        targetSdkVersion 27
//        versionCode System.getenv("APPCENTER_BUILD_ID") as Integer ?: 54
        versionCode 60
        versionName "alpha_b${versionCode}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "CLIENT_ID", '"' + System.getenv("ONLINE_GO_CLIENT_ID") + '"'
        buildConfigField "String", "CLIENT_SECRET", '"' + System.getenv("ONLINE_GO_CLIENT_SECRET") + '"'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

// IMPORTANT!  Enables view caching in viewholders.
// See: https://github.com/Kotlin/KEEP/blob/master/proposals/android-extensions-entity-caching.md
androidExtensions {
    experimental = true
}

task generateGoogleJson {
    doLast {
        def output = new File("app/google-services.json")
        def input = new File("secret.bin")
        if (!output.exists()) {
            def inputBytes = input.bytes
            def outputBytes = new byte[inputBytes.size()]
            def passwordBytes = System.env.SECRET_KEY.bytes
            inputBytes.eachWithIndex { byte b, int i ->
                outputBytes[i] = b ^ passwordBytes[i % passwordBytes.size()]
            }
            output.bytes = outputBytes
        }
    }
}

preBuild.dependsOn(generateGoogleJson)

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // retrofit
    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.3.0"
    implementation "com.squareup.retrofit2:converter-gson:2.3.0"
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'

    implementation 'com.squareup.moshi:moshi-adapters:1.6.0'

    // rxandroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'

    // groupie
    implementation 'com.xwray:groupie:2.0.3'
    implementation 'com.xwray:groupie-kotlin-android-extensions:2.0.3'

    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation "com.android.support:support-emoji-appcompat:27.1.1"
    implementation "com.android.support:support-emoji:27.1.1"

    implementation 'de.hdodenhof:circleimageview:2.2.0'

    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'

    implementation('io.socket:socket.io-client:1.0.0') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }

    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

    implementation 'br.com.simplepass:loading-button-android:1.8.3'
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    implementation 'com.google.firebase:firebase-core:16.0.0'
    kapt 'com.github.bumptech.glide:compiler:4.4.0'

    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.3'

    implementation 'com.jakewharton:butterknife:8.8.1'
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'com.firebase:firebase-jobdispatcher-with-gcm-dep:0.8.4'

    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
    testImplementation 'junit:junit:4.12'

    implementation 'android.arch.persistence.room:runtime:1.1.1-rc1'
    implementation 'android.arch.persistence.room:rxjava2:1.1.1-rc1'
    kapt 'android.arch.persistence.room:compiler:1.1.1-rc1'
}
repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
