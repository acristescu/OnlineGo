apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'koin'
apply plugin: 'app.cash.molecule'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 33
    ndkVersion "21.0.6113669"
    defaultConfig {
        applicationId "io.zenandroid.onlinego"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 42
        versionName "alpha_b${versionCode}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        buildConfigField "String", "CLIENT_ID", '"' + System.getenv("ONLINE_GO_CLIENT_ID") + '"'
        buildConfigField "String", "CLIENT_SECRET", '"' + System.getenv("ONLINE_GO_CLIENT_SECRET") + '"'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += "-opt-in=androidx.compose.animation.ExperimentalAnimationApi"
            freeCompilerArgs += "-opt-in=androidx.compose.material.ExperimentalMaterialApi"
            freeCompilerArgs += "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi"
            freeCompilerArgs += "-opt-in=androidx.compose.ui.ExperimentalComposeUiApi"
            freeCompilerArgs += "-opt-in=com.google.accompanist.pager.ExperimentalPagerApi"
            freeCompilerArgs += '-Xjvm-default=all'
        }
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    buildFeatures {
        compose true
        viewBinding true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.3.2"
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
            version "3.10.2"
        }
    }
    namespace 'io.zenandroid.onlinego'
    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

dependencies {
    def koin_version = '3.2.0'
    def play_services_version = '20.4.1'
    def accompanist_version = '0.27.0'
    def room_version = '2.5.0'
    def firebase_version = '31.2.0'
    def emoji2_version = "1.2.0"
    def composeBom = platform('androidx.compose:compose-bom:2023.01.00')

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation composeBom
    implementation "androidx.compose.material:material"
    implementation 'androidx.compose.material:material-icons-extended'
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.ui:ui-util"
    debugImplementation "androidx.compose.ui:ui-tooling"

    implementation 'androidx.appcompat:appcompat:1.6.1'

    implementation platform("com.google.firebase:firebase-bom:$firebase_version")
    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

    implementation "com.google.android.gms:play-services-auth:$play_services_version"
    implementation "org.jetbrains.kotlinx:kotlinx-collections-immutable:0.3.5"

    implementation "io.coil-kt:coil-compose:2.1.0"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    implementation 'com.squareup.moshi:moshi-kotlin:1.13.0'
    implementation 'com.squareup.moshi:moshi-adapters:1.13.0'

    implementation "androidx.navigation:navigation-fragment-ktx:2.5.3"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.3"
    implementation "androidx.core:core-ktx:1.9.0"

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    implementation "androidx.work:work-runtime-ktx:2.8.0"
    implementation "androidx.work:work-rxjava2:2.8.0"

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    implementation 'com.facebook.stetho:stetho:1.6.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

    // groupie
    implementation 'com.github.lisawray.groupie:groupie:2.9.0'
    implementation 'com.github.lisawray.groupie:groupie-viewbinding:2.8.1'

    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.emoji2:emoji2:$emoji2_version"
    implementation "androidx.emoji2:emoji2-views:$emoji2_version"
    implementation "androidx.emoji2:emoji2-views-helper:$emoji2_version"
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation "androidx.activity:activity-compose:1.6.1"

    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'

    implementation('io.socket:socket.io-client:2.0.1') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }

    implementation 'com.jakewharton.rxbinding3:rxbinding:3.0.0'

    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-messaging'

    implementation 'io.noties.markwon:core:4.3.1'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    implementation "io.insert-koin:koin-android:$koin_version"

    implementation "com.android.billingclient:billing-ktx:5.1.0"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    implementation 'com.jakewharton.threetenabp:threetenabp:1.4.0'
    implementation 'com.github.cpiz:BubbleView:1.0.3'

    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"

    kapt "androidx.room:room-compiler:$room_version"

    implementation 'androidx.test.espresso:espresso-idling-resource:3.5.1'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.6.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-inline:3.11.2"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation "io.insert-koin:koin-test-junit4:$koin_version"

    androidTestImplementation composeBom
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
}
repositories {
    mavenCentral()
}